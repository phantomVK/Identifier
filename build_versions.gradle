ext {
    compileSdk = 34
    minSdk = 17
    targetSdk = 34
    versionCode = getVersionCode()
    versionName = getVersionName()
    gitRevision = getGitRevision()

    hms = '3.4.62.300'
    hms_opendevice = '6.12.0.300'
    mcs = '1.0.3.300'

    appcompact = '1.4.2'
    annotation = '1.3.0'
    junit = '4.13.2'
    android_junit = '1.0.0'
    espresso_core = '3.1.0'
}

def getVersionName() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        def ver = stdout.toString().trim()
        if (ver.contains('-')) {
            return ver.takeBefore('-')
        } else {
            return ver
        }
    } catch (ignored) {
        return "0.0.1"
    }
}

def getVersionCode() {
    def name = getVersionName()
    def code = 0
    name.split("\\.").each { code = code * 100 + it.toInteger() }
    return code
}

static def getGitRevision() {
    try {
        return "git rev-parse --short HEAD".execute().text.trim()
    } catch (ignored) {
        return "0000000"
    }
}