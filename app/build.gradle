plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace "com.phantomvk.identifier.app"
    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        multiDexEnabled true
        minSdk rootProject.ext.minSdk
        targetSdk rootProject.ext.targetSdk
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField "String", "GIT_REVISION", "\"${rootProject.ext.gitRevision}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def rootProject = rootProject.file("../config/keystore.properties")
            if (!rootProject.exists()) return

            def prop = new Properties()
            new FileInputStream(rootProject).withCloseable { res -> prop.load(res) }

            if (!prop.isEmpty()) {
                keyAlias prop['keyAlias']
                keyPassword prop['keyPassword']
                storePassword prop['storePassword']
                storeFile file(prop['keyStore'])
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFileName = "identifier" +
                            "_${rootProject.ext.versionName}" +
                            "_${rootProject.ext.gitRevision}" +
                            "_${variant.buildType.name}" +
                            ".apk" // identifier_0.8.12_88e1359_release.apk
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        exclude "DebugProbesKt.bin"
        exclude "androidsupportmultidexversion.txt"
    }
}

dependencies {
    implementation project(":library")
//    implementation "com.github.phantomVK:Identifier:latest.release@aar"

//    implementation("com.huawei.hms:ads-identifier:$hms")
//    implementation("com.hihonor.mcs:ads-identifier:$mcs")

    implementation "androidx.appcompat:appcompat:$appcompact"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"

    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    testImplementation "junit:junit:$junit"
    androidTestImplementation "androidx.test.ext:junit:$android_junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core"
}